apply plugin: 'com.android.library'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "4.4.5"
def min_plugin_version = "3.0.0"

android {
    compileSdkVersion 30

    resourcePrefix "sensors_analytics_"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        consumerProguardFiles 'proguard-rules.pro'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "SDK_VERSION", "\"${version}\"")
            buildConfigField("String", "MIN_PLUGIN_VERSION", "\"${min_plugin_version}\"")
            manifestPlaceholders = ["SENSORSDATA_SDK_VERSION": "${version}"]
        }

        debug {
            buildConfigField("String", "SDK_VERSION", "\"${version}\"")
            buildConfigField("String", "MIN_PLUGIN_VERSION", "\"${min_plugin_version}\"")
            manifestPlaceholders = ["SENSORSDATA_SDK_VERSION": "${version}"]
        }
    }

    testOptions {
        unitTests.all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:core:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    // Espresso dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.1.1'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.1.1'
    androidTestImplementation 'org.mockito:mockito-core:2.27.0'
    androidTestImplementation 'org.mockito:mockito-android:2.27.0'
    //Robolectric & JVM test
    testImplementation 'androidx.test:core:1.1.0'
    testImplementation 'androidx.test.ext:junit:1.1.0'
    testImplementation 'org.robolectric:robolectric:4.2'
    testImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    testImplementation 'androidx.test.espresso:espresso-contrib:3.1.1'
    testImplementation 'androidx.test.espresso:espresso-intents:3.1.1'
    testImplementation 'androidx.test.espresso:espresso-accessibility:3.1.1'
    testImplementation 'androidx.test.espresso:espresso-web:3.1.1'
    testImplementation 'androidx.test.espresso.idling:idling-concurrent:3.1.1'
    testImplementation 'org.robolectric:robolectric:4.2'
    //mock
    testImplementation 'org.mockito:mockito-core:2.27.0'
    testImplementation 'org.mockito:mockito-android:2.27.0'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.2'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.2'
    testImplementation 'org.powermock:powermock-module-testng:2.0.2'
    testImplementation 'org.powermock:powermock-core:2.0.2'

}

def siteUrl = 'https://github.com/sensorsdata/sa-sdk-android'
def gitUrl = 'https://github.com/sensorsdata/sa-sdk-android.git'
group = "com.sensorsdata.analytics.android"
project.archivesBaseName = "SensorsAnalyticsSDK"

install {
    repositories.mavenInstaller {
        // generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'The official Android SDK for Sensors Analytics.'
                description 'The official Android SDK for Sensors Analytics.'
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'zouyuhan'
                        name 'Yuhan ZOU'
                        email 'zouyuhan@sensorsdata.cn'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    dependsOn 'assembleRelease'
    source = android.sourceSets.main.java.srcDirs +
            'build/generated/not_namespaced_r_class_sources/release/generateReleaseRFile/out/com/sensorsdata/analytics/android/sdk/R.java'
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task makeJar(type: Copy) {
    def jarName = 'SensorsAnalytics-Android-SDK-Release-' + version + '.jar'
    delete 'build/libs/' + jarName
    from('build/intermediates/packaged-classes/release/')
    into('build/libs/')
    include('classes.jar')
    rename('classes.jar', jarName)
}

makeJar.dependsOn(build)

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    def mavenUser = properties.getProperty("maven.user")
    def mavenPassword = properties.getProperty("maven.password")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "SensorsAnalyticsSDK"
        desc = 'The official Android SDK for Sensors Analytics.'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        labels = ['android']
        publish = true
        publicDownloadNumbers = true
        version {

            mavenCentralSync {
                sync = true
                //Optional (true by default). Determines whether to sync the version to Maven Central.
                user = mavenUser //OSS user token
                password = mavenPassword //OSS user password
                close = '1'
                //Optional property. By default the staging repository is closed and artifacts are released to Maven Central.
                // You can optionally turn this behaviour off (by putting 0 as value) and release the version manually.
            }
        }

    }
}